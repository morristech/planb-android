apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def final publishConfig = [
        name                : 'Plan B',
        group               : 'at.favre.lib',
        siteUrl             : 'https://github.com/patrickfav/planb-android',
        gitUrl              : 'https://github.com/patrickfav/planb-android.git',
        issueTrackerUrl     : 'https://github.com/patrickfav/planb-android/issues',
        githubRepo          : 'patrickfav/planb-android',

        descriptionText     : 'A crash recovery library for Android. It allows tracking and handling crashes with different rules for debugging and production.',

        syncWithMavenCentral: true,
        uploadDryRun        : true,
        uploadAutoPublish   : true,
        uploadOverride      : true,

        pomDevId            : 'pfavre',
        pomDevName          : 'Patrick Favre-Bulle',
        pomDevMail          : 'patrick.favrebulle@gmail.com',

        licenseName         : 'The Apache Software License, Version 2.0',
        licenseBintray      : 'Apache-2.0',
        licenseUrl          : 'http://www.apache.org/licenses/LICENSE-2.0.txt',

        bintrayLabels       : ['android', 'crash', 'crash-reporting', 'crash-recovery', 'android-library']
]

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            version rootProject.ext.versionNameLib
            project {
                name publishConfig.name
                group publishConfig.group
                packaging 'aar'
                description publishConfig.descriptionText
                url publishConfig.siteUrl

                // Set your license
                licenses {
                    license {
                        name publishConfig.licenseName
                        url publishConfig.licenseUrl
                    }
                }
                developers {
                    developer {
                        id publishConfig.pomDevId
                        name publishConfig.pomDevName
                        email publishConfig.pomDevMail
                    }
                }
                scm {
                    connection publishConfig.gitUrl
                    developerConnection publishConfig.gitUrl
                    url publishConfig.siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    dryRun = publishConfig.uploadDryRun
    publish = publishConfig.uploadAutoPublish
    //Whether version should be auto published after an upload
    override = publishConfig.uploadOverride
    //[Default: false] Whether to override version artifacts already published

    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = project.buildDir.getParentFile().name
        desc = publishConfig.descriptionText
        issueTrackerUrl = publishConfig.issueTrackerUrl
        websiteUrl = publishConfig.siteUrl
        vcsUrl = publishConfig.gitUrl
        licenses = [publishConfig.licenseBintray]
        githubRepo = publishConfig.githubRepo
        githubReleaseNotesFile = 'CHANGELOG'
        labels = publishConfig.bintrayLabels

        version {

            name = rootProject.ext.versionNameLib
            vcsTag = rootProject.ext.versionNameLib
            released = new Date()

            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
            }

            mavenCentralSync {
                sync = publishConfig.syncWithMavenCentral
                //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = properties.getProperty("bintray.oss.user") //OSS user token
                password = properties.getProperty("bintray.oss.password") //OSS user password
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}